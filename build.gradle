import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    alias(libs.plugins.th2.publish)
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.kotlin.kapt)
    alias(libs.plugins.protobuf)
    alias(libs.plugins.jmh)
    id "java-library"
    id "java-test-fixtures"
    id "maven-publish"
}

group = 'com.exactpro.th2'
version = release_version

ext {
    genBaseDir = project.extensions.findByName("protobuf").defaultGeneratedFilesBaseDir.get()
}

repositories {
    mavenCentral()
    maven {
        name 'Sonatype_releases'
        url 'https://repo1.maven.org/maven2'
    }
    mavenLocal()

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.freeCompilerArgs += "-Xjvm-default=all"
    compilerOptions {
        // The next instruction should be deleted after migration to kotlin 1.9.+
        kotlinDaemonJvmArguments.addAll("--add-exports=jdk.compiler/com.sun.tools.javac.main=ALL-UNNAMED")
    }
}

test {
    useJUnitPlatform {
        excludeTags('integration-test')
    }
}

tasks.register('integrationTest', Test) {
    group = 'verification'
    useJUnitPlatform {
        includeTags('integration-test')
    }
}

dependencies {
    api(platform(libs.th2.bom)) {
        because('th2 bom includes version for third party libraries')
    }
    api(libs.th2.grpc.common) {
        because('protobuf transport is main now, this dependency should be moved to grpc, mq protobuf modules after splitting')
    }
    api(libs.th2.cradle.core) {
        because('cradle is included into common library now, this dependency should be moved to a cradle module after splitting')
    }
    api('io.netty:netty-buffer') {
        because('th2 transport protocol is included into common library now, this dependency should be moved to a th2 transport module after splitting')
    }

    jmh libs.jmh.core
    jmh libs.jmh.generator.annprocess

    implementation 'com.google.protobuf:protobuf-java-util'
    implementation libs.th2.grpc.service.generator
    implementation libs.th2.cradle.cassandra

    implementation libs.auto.value.annotations
    kapt(libs.auto.value)
    kapt("org.jetbrains.kotlin:kotlin-metadata-jvm")
    //this is required to add generated bridge classes for kotlin default constructors
    implementation(files(project.layout.buildDirectory.file("tmp/kapt3/classes/main").get().asFile))

    //FIXME: Add these dependencies as api to grpc-... artifacts
    implementation "io.grpc:grpc-protobuf"
    implementation "io.grpc:grpc-core"
    implementation "io.grpc:grpc-netty"
    implementation "io.grpc:grpc-services"

    implementation "com.rabbitmq:amqp-client"

    implementation "org.jetbrains:annotations"

    implementation "org.apache.commons:commons-lang3"
    implementation "org.apache.commons:commons-collections4"
    implementation "org.apache.commons:commons-text"
    implementation("commons-io:commons-io") {
        because('we need FilenameUtil to use wildcard matching')
    }
    implementation "commons-cli:commons-cli"
    implementation "commons-io:commons-io"

    implementation "com.fasterxml.jackson.core:jackson-core"
    implementation("com.fasterxml.jackson.core:jackson-databind") {
        because('provide ability to use object mapper in components')
    }
    implementation("com.fasterxml.jackson.core:jackson-annotations") {
        because('providee ability to use jackson annotations in components')
    }
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor'

    implementation(libs.java.uuid.generator)

    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl'
    implementation 'org.apache.logging.log4j:log4j-core'

    implementation 'io.prometheus:simpleclient'
    implementation 'io.prometheus:simpleclient_hotspot'
    implementation 'io.prometheus:simpleclient_httpserver'
    implementation 'io.prometheus:simpleclient_log4j2'

    implementation(libs.kubernetes.client) {
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-yaml'
    }

    implementation libs.kotlin.logging

    testImplementation libs.junit.jupiter
    testImplementation libs.mockito.kotlin
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation libs.annotation.api
    testImplementation libs.testcontainers
    testImplementation libs.testcontainers.rabbitmq
    testImplementation(libs.junit.pioneer) {
        because("system property tests")
    }
    testImplementation libs.awaitility

    testRuntimeOnly(libs.kotlinx.coroutines.core)

    testFixturesImplementation libs.kotlin.test.junit5
    testFixturesImplementation libs.junit.jupiter
}

sourceSets {
    main.kotlin.srcDirs += "src/main/kotlin"
    test.resources.srcDirs += "$genBaseDir/test/services/java/resources"
}

protobuf {
    protoc {
        artifact = "${libs.protobuf.protoc.get()}"
    }
    plugins {
        grpc {
            artifact = "${libs.protoc.gen.grpc.java.get()}"
        }
        services {
            artifact = "${libs.th2.grpc.service.generator.get()}:all@jar"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            services {
                option 'javaInterfacesPath=./java/src'
                option 'javaInterfacesImplPath=./java/src'
                option 'javaMetaInfPath=./java/resources'
            }
        }
        ofSourceSet('test')
    }
}

tasks.named('generateProto') { enabled = false }
tasks.named('extractIncludeProto') { enabled = false }
tasks.named('extractIncludeTestProto') { enabled = false }
tasks.named('extractIncludeTestFixturesProto') { enabled = false }

compileTestJava.dependsOn.add('generateTestProto')
processTestResources.dependsOn.add('generateTestProto')
generatePomFileForMavenJavaPublication.dependsOn.add('kaptKotlin')

tasks.register("publicationManualBench", JavaExec.class) {
    mainClass.set('com.exactpro.th2.common.schema.message.impl.rabbitmq.connection.ConnectionManualBenchmark')
    classpath(sourceSets.test.runtimeClasspath)
    dependsOn('testClasses')

    jvmArgs('-XX:StartFlightRecording=duration=60s,settings=profile,filename=publishing-profile-record.jfr')
}