buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }

    ext {
        kotlin_version = "1.3.72"
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}

plugins {
    id 'com.google.protobuf' version '0.8.8' apply false
    id "com.jfrog.bintray" version '1.8.5'
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    ext {
        sharedDir           = file("${project.rootDir}/shared")
    }

    group = 'com.exactpro.th2'
    printf('Branch name %s, build number %s', branch_name, build_num)
    version = release_version

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        maven {
            name 'MavenLocal'
            url sharedDir
        }
        mavenCentral()
        maven {
            name 'Bintray'
            url 'https://dl.bintray.com/exactpro/th2'
        }
        mavenLocal()

        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        (options as StandardJavadocDocletOptions).addBooleanOption("html5", true)
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
            bintrayPublication(MavenPublication) {
                from components.java
                groupId group
                artifactId rootProject.name
                version project.version
                pom.withXml {
                    def root = asNode()
                    root.appendNode('name', rootProject.name)
                    root.appendNode('url', project.vcs_url)
                }
            }
        }

        repositories {
            maven {
                name = 'localRepo'
                url = sharedDir
            }
        }
    }

    bintray {
        user = "${bintray_user}"
        key = "${bintray_key}"
        publications = ['bintrayPublication']
        pkg {
            repo = 'th2'
            name = rootProject.name
            userOrg = 'exactpro'
            licenses = ['Apache-2.0']
            vcsUrl = project.vcs_url
            version {
                name = project.version
                desc = project.version
                released  = new Date()
            }
        }
        publish = true
    }
}

ext {
    cradleVersion       = '2.6.1'
    junitVersion      = '5.4.2'
}

test {
    useJUnitPlatform()
}

dependencies {
    api platform('com.exactpro.th2:bom:2.10.1')
    api "com.exactpro.th2:cradle-core:${cradleVersion}"
    api 'com.exactpro.th2:grpc-common:2.3.2'

    implementation 'com.google.protobuf:protobuf-java-util'
    implementation 'com.exactpro.th2:grpc-service-generator:3.1.2'
    implementation "com.exactpro.th2:cradle-cassandra:${cradleVersion}"

    //FIXME: Adde these dependnecies as api to grpc-... artifaccts
    implementation "com.google.protobuf:protobuf-java-util"
    implementation "io.grpc:grpc-protobuf"
    implementation "io.grpc:grpc-stub"
    implementation "io.grpc:grpc-core"
    implementation "io.grpc:grpc-netty"

    implementation "com.rabbitmq:amqp-client"

    implementation "org.jetbrains:annotations"

    implementation "org.apache.commons:commons-lang3"
    implementation "org.apache.commons:commons-collections4:4.4"
    implementation "org.apache.commons:commons-text:1.9"
    implementation "commons-cli:commons-cli:1.4"

    implementation "com.fasterxml.jackson.core:jackson-core"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.core:jackson-annotations"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"

    implementation 'com.fasterxml.uuid:java-uuid-generator:4.0.1'

    implementation "org.slf4j:slf4j-log4j12"
    implementation "org.slf4j:slf4j-api"

    implementation 'io.prometheus:simpleclient'
    implementation 'io.prometheus:simpleclient_hotspot'
    implementation 'io.prometheus:simpleclient_httpserver'

    implementation 'io.fabric8:kubernetes-client:4.13.0'

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

jar {
    manifest {
        attributes(
                'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title':      '',
                'Specification-Vendor':     'Exactpro Systems LLC',
                'Implementation-Title':     project.archivesBaseName,
                'Implementation-Vendor':    'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version':   project.version
        )
    }
}

sourceSets {
    main.kotlin.srcDirs += "src/main/kotlin"
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
