import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    alias(libs.plugins.th2.publish)
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.kotlin.kapt)
    alias(libs.plugins.protobuf)
    alias(libs.plugins.jmh)
    id "java-library"
    id "java-test-fixtures"
    id "maven-publish"
}

group = 'com.exactpro.th2'
version = release_version

ext {
    genBaseDir = file("${buildDir}/generated/source/proto")
}

repositories {
    mavenCentral()
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
    mavenLocal()

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

kotlin {
    jvmToolchain(11)
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.freeCompilerArgs += "-Xjvm-default=all"
}

test {
    useJUnitPlatform {
        excludeTags('integration-test')
    }
}

tasks.register('integrationTest', Test) {
    group = 'verification'
    useJUnitPlatform {
        includeTags('integration-test')
    }
}

dependencies {
    api(libs.th2.grpc.common) {
        because('protobuf transport is main now, this dependency should be moved to grpc, mq protobuf modules after splitting')
    }
    api(libs.th2.cradle.core) {
        because('cradle is included into common library now, this dependency should be moved to a cradle module after splitting')
    }
    api('io.netty:netty-buffer') {
        because('th2 transport protocol is included into common library now, this dependency should be moved to a th2 transport module after splitting')
    }

    jmh libs.jmh.core
    jmh libs.jmh.generator.annprocess

    implementation 'com.google.protobuf:protobuf-java-util'
    implementation libs.th2.grpc.service.generator
    implementation libs.th2.cradle.cassandra

    implementation libs.auto.value.annotations
    kapt(libs.auto.value) {
        //FIXME: Updated library because it is fat jar
        // auto-value-1.10.1.jar/META-INF/maven/com.google.guava/guava/pom.xml (pkg:maven/com.google.guava/guava@31.1-jre, cpe:2.3:a:google:guava:31.1:*:*:*:*:*:*:*) : CVE-2023-2976, CVE-2020-8908
    }
    //this is required to add generated bridge classes for kotlin default constructors
    implementation(files("$buildDir/tmp/kapt3/classes/main"))

    //FIXME: Add these dependencies as api to grpc-... artifacts
    implementation "io.grpc:grpc-protobuf"
    implementation "io.grpc:grpc-core"
    implementation "io.grpc:grpc-netty"
    implementation "io.grpc:grpc-services"

    implementation "com.rabbitmq:amqp-client"

    implementation "org.jetbrains:annotations"

    implementation "org.apache.commons:commons-lang3"
    implementation "org.apache.commons:commons-collections4"
    implementation "org.apache.commons:commons-text"
    implementation("commons-io:commons-io") {
        because('we need FilenameUtil to use wildcard matching')
    }
    implementation "commons-cli:commons-cli"
    implementation "commons-io:commons-io"

    implementation "com.fasterxml.jackson.core:jackson-core"
    implementation("com.fasterxml.jackson.core:jackson-databind") {
        because('provide ability to use object mapper in components')
    }
    implementation("com.fasterxml.jackson.core:jackson-annotations") {
        because('providee ability to use jackson annotations in components')
    }
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor'

    implementation "com.fasterxml.uuid:java-uuid-generator:5.1.0"

    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl'
    implementation 'org.apache.logging.log4j:log4j-core'

    implementation 'io.prometheus:simpleclient'
    implementation 'io.prometheus:simpleclient_hotspot'
    implementation 'io.prometheus:simpleclient_httpserver'
    implementation 'io.prometheus:simpleclient_log4j2'

    implementation(libs.okio) {
        because('fix vulnerability in transitive dependency <com.squareup.okhttp3:okhttp>' +
                'Note: kotlin:1.9 is transitive dependency since 3.5.0')
    }
    implementation(libs.okhttp3.okhttp) {
        because('fix vulnerability in transitive dependency <kubernetes-client>. ' +
                'Note: kotlin:1.9 is transitive dependency since 4.12.0')
    }
    implementation(libs.okhttp3.logging.interceptor) {
        because('fix vulnerability in transitive dependency <kubernetes-client>. ' +
                'Note: kotlin:1.9 is transitive dependency since 4.12.0')
    }

    implementation(libs.kubernetes.client) {
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-yaml'
    }

    implementation libs.kotlin.logging

    testImplementation libs.javax.annotation.api
    testImplementation libs.junit.jupiter
    testImplementation libs.mockito.kotlin
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation libs.testcontainers
    testImplementation libs.testcontainers.rabbitmq
    testImplementation(libs.junit.pioneer) {
        because("system property tests")
    }
    testImplementation libs.awaitility

    testFixturesImplementation libs.kotlin.test.junit5
    testFixturesImplementation libs.junit.jupiter
}

// substitute commons-compress:1.24.0 from testcontainers:1.19.8 to version 1.26.2
// because commons-compress:1.24.0 contains vulnerabilities (CVE-2024-25710, CVE-2024-26308)
configurations {
    configureEach {
        resolutionStrategy {
            dependencySubstitution {
                substitute module('org.apache.commons:commons-compress') using module("${libs.commons.compress.get()}")
            }
        }
    }
}

sourceSets {
    main.kotlin.srcDirs += "src/main/kotlin"
    test.resources.srcDirs += "$genBaseDir/test/services/java/resources"
}

protobuf {
    protoc {
        artifact = "${libs.protobuf.protoc.get()}"
    }
    plugins {
        grpc {
            artifact = "${libs.protoc.gen.grpc.java.get()}"
        }
        services {
            artifact = "${libs.th2.grpc.service.generator.get()}:all@jar"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            services {
                option 'javaInterfacesPath=./java/src'
                option 'javaInterfacesImplPath=./java/src'
                option 'javaMetaInfPath=./java/resources'
            }
        }
        ofSourceSet('test')
    }
}

tasks.named('generateProto') { enabled = false }
tasks.named('extractIncludeProto') { enabled = false }
tasks.named('extractIncludeTestProto') { enabled = false }
tasks.named('extractIncludeTestFixturesProto') { enabled = false }

compileTestJava.dependsOn.add('generateTestProto')
processTestResources.dependsOn.add('generateTestProto')

tasks.register("publicationManualBench", JavaExec.class) {
    mainClass.set('com.exactpro.th2.common.schema.message.impl.rabbitmq.connection.ConnectionManualBenchmark')
    classpath(sourceSets.test.runtimeClasspath)
    dependsOn('testClasses')

    jvmArgs('-XX:StartFlightRecording=duration=60s,settings=profile,filename=publishing-profile-record.jfr')
}